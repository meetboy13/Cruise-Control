module CruiseControl:

input On;
input Off;
input Resume;
input Set;
input QuickAccel;
input QuickDecel;
input Accel : float;
input Brake : float;
input Speed : float;

output CruiseSpeed : float;
output ThrottleCmd : float;
output CruiseState : integer;

signal state : integer in
	[loop
		present state then
			emit CruiseState(?state);
			end present;
			pause;
	end loop]
	||
	run CruiseControlStateMachine[                           signal On/On1;
								 signal Off/Off1;
								 signal Resume/Resume1;
								 signal Brake/Brake1;
								 signal Accel/Accel1;
                                                                 signal Speed/Speed1;
								 signal state/CruiseState ]
	||
	run CruiseSpeedManagement[                         signal Set/Set2;
							   signal QuickDecel/QuickDecel2;
							   signal QuickAccel/QuickAccel2;
							   signal state/CruiseState2;
							   signal Speed/Speed2;
                                                           signal Accel/Accel2;
							   signal CruiseSpeed/CruiseSpeed2 ]
	||
	run CarDrivingControl[                          signal Accel/Accel3;
							signal CruiseSpeed/CruiseSpeed3;
							signal state/CruiseState3;
							signal Speed/Speed3;
							signal ThrottleCmd/ThrottleCmd3 ]
end signal

end module

module CruiseControlStateMachine:

constant PedalsMin : float;
constant SpeedMin : float;
constant SpeedMax : float;

input On1;
input Off1;
input Resume1;
input Accel1 : float;
input Speed1 : float;
input Brake1 : float;


output CruiseState : integer;

% OFF-1 ON-2 STDBY-3 DIS-4
var state := 1 : integer in
	loop
		emit CruiseState(state);
		pause;
		trap T2 in
			% OFF STATE LOGIC
			if state = 1 then
				present On1 then
					state := 2;
				end present;
				exit T2;
			end if;
			% ON STATE LOGIC
			if state = 2 then
				present Off1 then 
					state := 1; exit T2;
				else
					if (?Brake1 >= PedalsMin) then
						state := 3; exit T2;
					elsif (?Accel1 >= PedalsMin) or (?Speed1 < SpeedMin) or (?Speed1 > SpeedMax) then
						state := 4; exit T2;
					end if;
				end present;
				exit T2;
			end if;
			% STANDBY STATE LOGIC
			if state = 3 then
				present Off1 then
					state := 1; exit T2;
				else
					if (?Brake1 >= PedalsMin) then
						exit T2;
					elsif (?Speed1 >= SpeedMin) and (?Speed1 <= SpeedMax) and (?Accel1 < PedalsMin) then
						present Resume1 then
							state := 2;
						end present;
						exit T2;
					else
						present Resume1 then
							state := 4;
						end present;
						exit T2;
					end if;
				end present;
				exit T2;
			end if;
			% DISABLE STATE LOGIC
			if state = 4 then
				present Off1 then
					state := 1; exit T2;
				else
					if (?Brake1 >= PedalsMin) then
						state := 3; exit T2;
					elsif (?Accel1 < PedalsMin) and (?Speed1 >= SpeedMin) and (?Speed1 <= SpeedMax) then
						state := 2; exit T2;
					end if;
				end present;
				exit T2;
			end if;
		end trap
	end loop
end var
end module

module CruiseSpeedManagement:

constant SpeedMin : float;
constant SpeedMax : float;
constant SpeedInc : float;
constant Zero : float;

input Set2;
input QuickDecel2;
input QuickAccel2;
input Accel2 : float;
input CruiseState2 : integer;

output CruiseSpeed2 : float;
input Speed2 : float;

var CurrentSpeed := Zero : float in
	loop
		emit CruiseSpeed2(CurrentSpeed);
		pause;
		trap T3 in
			% OFF STATE LOGIC
			if ?CruiseState2 = 1 then
				CurrentSpeed := Zero;
				exit T3;
			% ALL OTHER STATES
			else
				present Set2 then
					CurrentSpeed := ?Speed2;
				end present;
				present QuickAccel2 then
					CurrentSpeed := CurrentSpeed + SpeedInc;
				end present;
				present QuickDecel2 then
					CurrentSpeed := CurrentSpeed - SpeedInc;
				end present;
				% IF JUST SWITCHED TO ON STATE FROM OFF STATE, LOAD CURRENT SPEED
				if ((?CruiseState2 = 2) and (pre(?CruiseState2) = 1)) then
					CurrentSpeed := ?Speed2;
				end if;
				if (CurrentSpeed > SpeedMax) then
					CurrentSpeed := SpeedMax;
				elsif (CurrentSpeed < SpeedMin) then
					CurrentSpeed := SpeedMin;
				end if;
				exit T3;
			end if;
		end trap
	end loop
end var
end module

module CarDrivingControl:

constant PedalsMin : float;
constant Zero : float;

input Accel3 : float;
input CruiseSpeed3 : float;
input CruiseState3 : integer;
input Speed3 : float;
output ThrottleCmd3 : float;

function regulateThrottle(integer, float, float) : float;

emit ThrottleCmd3(Zero);
loop
	pause;
	trap T3 in
		% IF JUST SWITCHED TO ON STATE, RESET INTEGRAL ACTION
		if ((?CruiseState3 = 2) and not(pre(?CruiseState3) = 2)) then
			emit ThrottleCmd3(regulateThrottle(1, ?CruiseSpeed3, ?Speed3));
			exit T3;
		% ALREADY IN ON STATE
		elsif ?CruiseState3 = 2 then
			emit ThrottleCmd3(regulateThrottle(0, ?CruiseSpeed3, ?Speed3));
			exit T3;
		% IN STANDBY OR DISABLE BYPASS CRUISECONTROL
		else
			if (?Accel3>PedalsMin) then
				emit ThrottleCmd3(?Accel3);
			else
				emit ThrottleCmd3(Zero);
			end if;
			exit T3;
		end if;
	end trap
end loop
end module
